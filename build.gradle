plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.3'
    // distribution
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    // ide
    id 'idea'
    id 'eclipse'
    // test coverage
    id 'jacoco'
    // quality
    id 'org.sonarqube' version '2.5'
    // xenon --version reads version from this file
    id 'de.fuerstenau.buildconfig' version '1.1.8'
}

version = '1.0.0'
applicationName = 'xenon-grpc'
group = 'nl.esciencecenter.xenon.grpc'
mainClassName = 'nl.esciencecenter.xenon.grpc.XenonServerWrapper'
ext.grpcVersion = '1.7.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

ext.xenonLibVersion = '2.0.0'

dependencies {
    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile "nl.esciencecenter.xenon:xenon:${xenonLibVersion}"
    compile 'net.sourceforge.argparse4j:argparse4j:0.8.1'
    compile 'io.netty:netty-tcnative-boringssl-static:2.0.6.Final'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.11.0'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.1'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.4.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

// Inform IntelliJ projects about the generated code. See https://github.com/google/protobuf-gradle-plugin#intellij-idea
idea {
    module {
        // Not using generatedSourceDirs because of
        // https://discuss.gradle.org/t/support-for-intellij-2016/15294/8
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/java")
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/grpc")
    }
}

// use shadow tasks everywhere
distZip.enabled = false
distTar.enabled = false
installDist.enabled = false

jacocoTestReport {
    // Exclude classes of generated source code by protoc
    // See https://stackoverflow.com/questions/29887805/filter-jacoco-coverage-reports-with-gradle
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['nl/esciencecenter/xenon/grpc/XenonProto*',
                                        'nl/esciencecenter/xenon/grpc/*ServiceGrpc*'])
        })
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarqube.com'
        property 'sonar.organization', project.group
        property 'sonar.language', 'java'
        property 'sonar.links.homepage', 'https://github.com/NLeSC/xenon-grpc'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}

buildConfig {
    appName = project.applicationName
    buildConfigField 'String', 'XENON_LIB_VERSION', "${xenonLibVersion}"
}
